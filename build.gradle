plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar { enabled = false }
jar { enabled = true }

allprojects {
    group = 'com.kor.syh'
    version = '0.0.1-SNAPSHOT'
    java {
        sourceCompatibility = '17'
    }
    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'


    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    ext {
        set('springCloudVersion', "2022.0.5")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

}

ext {
    springCloudVersion = "2022.0.5"
    jjwtVersion = '0.11.5'
    tsidCreatorVersion = '5.2.6'
}

project(":common") {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation("com.fasterxml.jackson.core:jackson-databind:2.17.0")
        implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0")
        //JWT
        implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
    }
}

project(':notification-service') {
    dependencies {
        implementation project(':common')
        // Web & Websocket
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-websocket'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // Redis
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        // Configuration & Discovery
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // Messaging
        implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'

    }

}

project(':member-service') {
    dependencies {
        implementation project(":common")
        // Web
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.mysql:mysql-connector-j'
        testImplementation 'com.h2database:h2:2.2.224'

        // Security
        implementation 'org.springframework.boot:spring-boot-starter-security'
        testImplementation 'org.springframework.security:spring-security-test:6.2.3'


        // Configuration & Discovery
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // Messaging
        implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'

        // ID generator
        implementation 'com.github.f4b6a3:tsid-creator:' + tsidCreatorVersion


    }

}

project(':gateway-service') {
    dependencies {
        implementation project(":common")
        // Gateway
        implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

        // Configuration & Discovery
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // JSON Web Token
        implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: jjwtVersion
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: jjwtVersion
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: jjwtVersion

        // Messaging
        implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'

    }

}

project(':discovery-service') {
    dependencies {
        // Eureka Server
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'

    }

}

project(':config-service') {
    dependencies {
        // Config Server
        implementation 'org.springframework.cloud:spring-cloud-config-server'

        // Messaging
        implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'

    }

}

project(':chat-service') {
    dependencies {
        implementation project(":common")
        // Web & Websocket
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-websocket'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        // Configuration & Discovery
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // Security
        implementation 'org.springframework.security:spring-security-messaging'
        implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: jjwtVersion
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: jjwtVersion
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: jjwtVersion

        // ID generator
        implementation 'com.github.f4b6a3:tsid-creator:' + tsidCreatorVersion

        // Messaging
        implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'

        // Kafka
        implementation 'org.springframework.kafka:spring-kafka'

        // test
        testImplementation "org.testcontainers:testcontainers:1.19.7"
        testImplementation 'org.testcontainers:junit-jupiter:1.19.7'
        testImplementation "org.testcontainers:mongodb:1.19.7"
        testImplementation(testFixtures(project(":common")))


    }

}







